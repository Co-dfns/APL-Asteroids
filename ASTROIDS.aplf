ASTROIDS;_;LISTEN;dat;DRC;srv;hr;title;rc;obj;evt;dat;gui;lc
	;height;width;frame;frame_size;difficulty
	;ship_dir;ship_input;is_collision;msg;msg_color
	;coords;sizes;sprites;vels;dirs
	;CON;WSU;KdDN;KdUP;KdLT;KdRT;KuDN;KuUP;KuLT;KuRT;BUMP;TICK;XT;ERR;sWAT
	;SUCC;FAIL;rWAT;RENDER;REG;CLOSE;GODOWN;GOUP;GOLEFT;GORIGHT;DEAD
	;NODOWN;NOUP;NOLEFT;NORIGHT

'Conga'⎕CY'conga'
DRC←Conga.Init'ASTROIDS'
srv←DRC.Srv '' 'localhost' 4572 'http' ('Options' DRC.Options.(WSAutoUpgrade))

title←'Astroids - Copyright (c) 2025 by Aaron W. Hsu'
hr←⎕NEW'HtmlRenderer'(⊂'Caption' title)
hr.(Coord Size)←'ScaledPixel' (540 680)
_←'<!DOCTYPE html><html><head>'
_,←'<style>'
_,←'	body{overflow:hidden;margin:0px;padding:10px;background:#202020;}'
_,←'	#d{border:1px solid green;display:block;padding:0px;margin:0px auto;}'
_,←'</style>'
_,←'<script>'
_,←'var d, c, s;'
_,←'function init() {'
_,←'	d = document.getElementById("d");'
_,←'	c = d.getContext("2d");'
_,←'	s = new WebSocket("ws://localhost:4572/");'
_,←'	s.onmessage = (evt) => {eval(evt.data);};'
_,←'	window.onkeydown = handle_keydown;'
_,←'	window.onkeyup = handle_keyup;'
_,←'}'
_,←'function handle_keydown(evt) {'
_,←'	s.send("KD_" + evt.key);'
_,←'}'
_,←'function handle_keyup(evt) {'
_,←'	s.send("KU_" + evt.key);'
_,←'}'
_,←'function render(msg, color, diff, objs) {'
_,←'	for (let i = 0; i < objs.length; i++) {'
_,←'		objs[i][0] = document.getElementById(objs[i][0]);'
_,←'	}'
_,←'	var g = c.createLinearGradient(0,0,25,480);'
_,←'	g.addColorStop(1, "#000000");'
_,←'	g.addColorStop(0, diff);'
_,←'	c.clearRect(0, 0, d.width, d.height);'
_,←'	c.fillStyle = g;'
_,←'	c.fillRect(0, 0, 25, 480);'
_,←'	c.font = "50px Courier";'
_,←'	c.fillStyle = color;'
_,←'	for (let i = 0; i < objs.length; i++) {'
_,←'		const obj = objs[i];'
_,←'		c.drawImage(obj[0], obj[1], obj[2], obj[3], obj[4]);'
_,←'	}'
_,←'	c.fillText(msg, 75, 250);'
_,←'}'
_,←'window.onload = init;'
_,←'</script>'
_,←'</head><body>'
_,←'<display style="display:none;">'
_,←'	<img id="ship" src="https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj9D85s3F9cYeXstj3B-WAq4LkbwlP0MXEnQqChi6nCPujANeH9GBbzc5sKQXavr1puezQI5-cnVlj2uPq8UAZfWckb9zPiOUfJnf5vCBedQViySuBSQeLJdHKM8J5E96KWz2hJYuqXOhU/s287/blueship2.png">'
_,←'	<img id="alien" src="https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEi5IXayE0bMDvxRuQNVAD4Zg2L9FN9_nRRPUH8Z83_8zZyE9e7N8g4dRqcfyVTxssB52vGxcOE4LfGrFyhD0DnUEevBOPyL6XmHO3Oxjg5bHmnBim8wKQ56iunBlRdJxwm935gMlghqt6c/s302/aliensprite2.png">'
_,←'	<img id="expl" src="https://opengameart.org/sites/default/files/explosion0.png">'
_,←'</display>'
_,←'<canvas id="d" height="480" width="640">Loading...</canvas>'
_,←'</body></html>'
hr.HTML←_

CON WSU KdDN KdUP KdLT KdRT KuDN KuUP KuLT KuRT TICK BUMP XT ERR sWAT←1+⍳15
LISTEN←{rc←⊃z←DRC.Wait (1⊃srv) 15
	{0::1 ⋄ 0⊣hr.Active}⍬:		XT	⍝ Application closed by user
	is_collision⍬:			BUMP	⍝ Ship has hit something
	100=rc:				TICK	⍝ Timeout
	0≠rc:				ERR	⍝ Error in serv
	rc obj evt dat∘←z
	'Connect'≡evt:			CON	⍝ Connection from GUI
	'WSUpgrade'≡evt:		WSU	⍝ GUI WebSocketUpgrade
	'WSReceive'≡evt:{
		'KD_ArrowDown'≡⊃dat:	KdDN	⍝ GUI Keydown ArrowDown
		'KD_ArrowUp'≡⊃dat:	KdUP	⍝ GUI Keydown ArrowUp
		'KD_ArrowLeft'≡⊃dat:	KdLT	⍝ GUI Keydown ArrowLeft
		'KD_ArrowRight'≡⊃dat:	KdRT	⍝ GUI Keydown ArrowRight
		'KU_ArrowDown'≡⊃dat:	KuDN	⍝ GUI Keyup ArrowDown
		'KU_ArrowUp'≡⊃dat:	KuUP	⍝ GUI Keyup ArrowUp
		'KU_ArrowLeft'≡⊃dat:	KuLT	⍝ GUI Keyup ArrowLeft
		'KU_ArrowRight'≡⊃dat:	KuRT	⍝ GUI Keyup ArrowRight
		LISTEN⍬
	}⍵
	sWAT
}

frame_size←640 480 ⋄ frame←0
sprites←'ship' 'alien'
vels←4 1 ⋄ dirs←(0 0)(0 1) ⋄ coords←(300 200)(300 0) ⋄ sizes←(25 48)(50 64)
ship_input←0 0 0 0 ⋄ ship_dir←(0 1)(0 ¯1)(¯1 0)(1 0)
msg←'' ⋄ msg_color←0
difficulty←500

is_collision←{
	xb yb←0⊃coords ⋄ xe ye←xb yb+0⊃sizes
	xsb ysb←↓⍉↑1↓coords ⋄ xse yse←xsb ysb+↓⍉↑1↓sizes
	∨⌿(xb<xse)∧(xe>xsb)∧(yb<yse)∧(ye>ysb)
}

SUCC←{⎕←'Thanks for playing!'}
FAIL←{⎕←obj evt dat ⋄ ⎕←'Encountered an unrecoverable error. Exiting...'}
rWAT←{⎕←obj evt dat ⋄ ⎕←'Wat? This should not happen. SEND HELP!'}
REG←{gui∘←obj}
CLOSE←{_←DRC.Close gui}
GODOWN←{ship_input[0]←1} ⋄ NODOWN←{ship_input[0]←0}
GOUP←{ship_input[1]←1} ⋄ NOUP←{ship_input[1]←0}
GOLEFT←{ship_input[2]←1} ⋄ NOLEFT←{ship_input[2]←0}
GORIGHT←{ship_input[3]←1} ⋄ NORIGHT←{ship_input[3]←0}
DEAD←{
	vels×←0 ⋄ sprites[0]←⊂'expl' ⋄ sizes[0]←⊂100 128 ⋄ coords[0]+←⊂¯50 ¯64
	msg_color∘←0 ⋄ msg∘←'You''re dead, Jim.'
}
RENDER←{
	dirs[0]←+⌿ship_input×ship_dir
	difficulty∘←20⌈difficulty-1
	new_alien←'alien'(1+?7)({⍵=0+⍵}@1?0 0)((?⊃frame_size)0)(50 64)
	sprites vels dirs coords sizes,←(0=?difficulty)⌿∘⊂¨new_alien
	coords∘←⌊coords+vels×dirs
	sprites vels dirs coords sizes⌿⍨←⊂1@0⊢∧⌿¨(0≤coords)∧coords<⊂frame_size
	coords∘←(⊂frame_size)⌊0⌈coords
	args←⎕JSON (⊂¨sprites),¨coords,¨sizes
	color←'#',('0123456789ABCDEF'[16 16⊤msg_color∘←256|msg_color+10]),'0000'
	diff←'#',('0123456789ABCDEF'[16 16⊤⌊(256÷500)×500-difficulty]),'0000'
	1:_←DRC.Send gui (('render("',msg,'", "',color,'","',diff,'",',args,');') 1 1)
}

∆_:→⎕LC+LISTEN⍬
∆_CON:	REG⍬	⋄ →∆_CON_
∆_WSU:	rWAT⍬	⋄ ∘∘∘
∆_KdDN:	rWAT⍬	⋄ ∘∘∘
∆_KdUP:	rWAT⍬	⋄ ∘∘∘
∆_KdLT:	rWAT⍬	⋄ ∘∘∘
∆_KdRT:	rWAT⍬	⋄ ∘∘∘
∆_KuDN:	rWAT⍬	⋄ ∘∘∘
∆_KuUP:	rWAT⍬	⋄ ∘∘∘
∆_KuLT:	rWAT⍬	⋄ ∘∘∘
∆_KuRT:	rWAT⍬	⋄ ∘∘∘
∆_TICK:		⋄ →∆_
∆_BUMP:	rWAT⍬	⋄ ∘∘∘
∆_XT:	SUCC⍬	⋄ →0
∆_ERR:	FAIL⍬	⋄ ∘∘∘
∆_WAT:	rWAT⍬ 	⋄ ∘∘∘

∆_CON_:→⎕LC+LISTEN⍬
∆_CON_CON:	CLOSE⍬ ⋄ REG⍬	⋄ →∆_CON_
∆_CON_WSU:	RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_KdDN:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdUP:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdLT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdRT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuDN:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuUP:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuLT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuRT:	rWAT⍬		⋄ ∘∘∘
∆_CON_TICK:			⋄ →∆_CON_
∆_CON_BUMP:	rWAT⍬		⋄ ∘∘∘
∆_CON_XT:	SUCC⍬		⋄ →0
∆_CON_ERR:	FAIL⍬		⋄ ∘∘∘
∆_CON_WAT:	rWAT⍬ 		⋄ ∘∘∘

∆_CON_WSU_:→⎕LC+LISTEN⍬
∆_CON_WSU_CON:	CLOSE⍬ ⋄ REG⍬		⋄ →∆_CON_
∆_CON_WSU_WSU:	rWAT⍬			⋄ ∘∘∘
∆_CON_WSU_KdDN:	GODOWN⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KdUP:	GOUP⍬ ⋄ RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_WSU_KdLT:	GOLEFT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KdRT:	GORIGHT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuDN:	NODOWN⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuUP:	NOUP⍬ ⋄ RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_WSU_KuLT:	NOLEFT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuRT:	NORIGHT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_TICK:	RENDER⍬			⋄ →∆_CON_WSU_
∆_CON_WSU_BUMP:	DEAD⍬ ⋄ RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_XT:	SUCC⍬			⋄ →0
∆_CON_WSU_ERR:	FAIL⍬			⋄ ∘∘∘
∆_CON_WSU_WAT:	rWAT⍬ 			⋄ ∘∘∘

∆_CON_WSU_BUMP_:→⎕LC+LISTEN⍬
∆_CON_WSU_BUMP_CON:	CLOSE⍬ ⋄ REG⍬	⋄ →∆_CON_
∆_CON_WSU_BUMP_WSU:	rWAT⍬		⋄ ∘∘∘
∆_CON_WSU_BUMP_KdDN:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KdUP:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KdLT:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KdRT:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KuDN:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KuUP:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KuLT:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_KuRT:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_TICK:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_BUMP:	RENDER⍬		⋄ →∆_CON_WSU_BUMP_
∆_CON_WSU_BUMP_XT:	SUCC⍬		⋄ →0
∆_CON_WSU_BUMP_ERR:	FAIL⍬		⋄ ∘∘∘
∆_CON_WSU_BUMP_WAT:	rWAT⍬ 		⋄ ∘∘∘
