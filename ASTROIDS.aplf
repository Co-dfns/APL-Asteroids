ASTROIDS;_;LISTEN;dat;DRC;srv;hr;title;rc;obj;evt;dat;gui;lc
	;height;width;frame;frame_size
	;ship_vel;ship_pos;ship_input;ship_dir;ship_sprite;ship_width
	;CON;WSU;KdDN;KdUP;KdLT;KdRT;KuDN;KuUP;KuLT;KuRT;TICK;XT;ERR;sWAT
	;SUCC;FAIL;rWAT;RENDER;REG;CLOSE;GODOWN;GOUP;GOLEFT;GORIGHT
	;NODOWN;NOUP;NOLEFT;NORIGHT

'Conga'⎕CY'conga'
DRC←Conga.Init'ASTROIDS'
srv←DRC.Srv '' 'localhost' 4572 'http' ('Options' DRC.Options.(WSAutoUpgrade))

title←'Astroids - Copyright (c) 2025 by Aaron W. Hsu'
hr←⎕NEW'HtmlRenderer'(⊂'Caption' title)
hr.(Coord Size)←'ScaledPixel' (540 680)
_←'<!DOCTYPE html><html><head>'
_,←'<style>'
_,←'	body{overflow:hidden;margin:0px;padding:10px;background:#202020;}'
_,←'	#d{border:1px solid green;display:block;padding:0px;margin:0px auto;}'
_,←'</style>'
_,←'<script>'
_,←'var d, c, s;'
_,←'function init() {'
_,←'	d = document.getElementById("d");'
_,←'	c = d.getContext("2d");'
_,←'	s = new WebSocket("ws://localhost:4572/");'
_,←'	s.onmessage = (evt) => {eval(evt.data);};'
_,←'	window.onkeydown = handle_keydown;'
_,←'	window.onkeyup = handle_keyup;'
_,←'}'
_,←'function handle_keydown(evt) {'
_,←'	s.send("KD_" + evt.key);'
_,←'}'
_,←'function handle_keyup(evt) {'
_,←'	s.send("KU_" + evt.key);'
_,←'}'
_,←'function render(sp, x, y, w, h) {'
_,←'	c.clearRect(0, 0, d.width, d.height);'
_,←'	const img = document.getElementById(sp);'
_,←'	c.drawImage(img, x, y, w, h);'
_,←'}'
_,←'window.onload = init;'
_,←'</script>'
_,←'</head><body>'
_,←'<display style="display:none;">'
_,←'	<img id="s0" src="https://blogger.googleusercontent.com/img/b/R29vZ2xl/AVvXsEj9D85s3F9cYeXstj3B-WAq4LkbwlP0MXEnQqChi6nCPujANeH9GBbzc5sKQXavr1puezQI5-cnVlj2uPq8UAZfWckb9zPiOUfJnf5vCBedQViySuBSQeLJdHKM8J5E96KWz2hJYuqXOhU/s287/blueship2.png">'
_,←'</display>'
_,←'<canvas id="d" height="480" width="640">Loading...</canvas>'
_,←'</body></html>'
hr.HTML←_

CON WSU KdDN KdUP KdLT KdRT KuDN KuUP KuLT KuRT TICK XT ERR sWAT←1+⍳14
LISTEN←{rc←⊃z←DRC.Wait (1⊃srv) 15
	{0::1 ⋄ 0⊣hr.Active}⍬:		XT	⍝ Application closed by user
	100=rc:				TICK	⍝ Timeout
	0≠rc:				ERR	⍝ Error in serv
	rc obj evt dat∘←z
	'Connect'≡evt:			CON	⍝ Connection from GUI
	'WSUpgrade'≡evt:		WSU	⍝ GUI WebSocketUpgrade
	'WSReceive'≡evt:{
		'KD_ArrowDown'≡⊃dat:	KdDN	⍝ GUI Keydown ArrowDown
		'KD_ArrowUp'≡⊃dat:	KdUP	⍝ GUI Keydown ArrowUp
		'KD_ArrowLeft'≡⊃dat:	KdLT	⍝ GUI Keydown ArrowLeft
		'KD_ArrowRight'≡⊃dat:	KdRT	⍝ GUI Keydown ArrowRight
		'KU_ArrowDown'≡⊃dat:	KuDN	⍝ GUI Keydown ArrowDown
		'KU_ArrowUp'≡⊃dat:	KuUP	⍝ GUI Keydown ArrowUp
		'KU_ArrowLeft'≡⊃dat:	KuLT	⍝ GUI Keydown ArrowLeft
		'KU_ArrowRight'≡⊃dat:	KuRT	⍝ GUI Keydown ArrowRight
		LISTEN⍬
	}⍵
	sWAT
}

frame_size←640 480 ⋄ frame←0
ship_sprite←'s0' ⋄ ship_width←25 48 ⋄ ship_vel←3 ⋄ ship_pos←300 200
ship_input←0 0 0 0 ⋄ ship_dir←(0 1)(0 ¯1)(¯1 0)(1 0)

SUCC←{⎕←'Thanks for playing!'}
FAIL←{⎕←obj evt dat ⋄ ⎕←'Encountered an unrecoverable error. Exiting...'}
rWAT←{⎕←obj evt dat ⋄ ⎕←'Wat? This should not happen. SEND HELP!'}
REG←{gui∘←obj}
CLOSE←{_←DRC.Close gui}
GODOWN←{ship_input[0]←1} ⋄ NODOWN←{ship_input[0]←0}
GOUP←{ship_input[1]←1} ⋄ NOUP←{ship_input[1]←0}
GOLEFT←{ship_input[2]←1} ⋄ NOLEFT←{ship_input[2]←0}
GORIGHT←{ship_input[3]←1} ⋄ NORIGHT←{ship_input[3]←0}
RENDER←{
	ship_pos⊢←frame_size|⌊ship_pos+ship_vel×⊃+⌿ship_input×ship_dir
	args←{⍺,',',⍵}⌿⍕¨(⊂'"',ship_sprite,'"'),ship_pos,ship_width
	1:_←DRC.Send gui ((⍕'render(',args,');') 1 1)
}

∆_:→⎕LC+LISTEN⍬
∆_CON:	REG⍬	⋄ →∆_CON_
∆_WSU:	rWAT⍬	⋄ ∘∘∘
∆_KdDN:	rWAT⍬	⋄ ∘∘∘
∆_KdUP:	rWAT⍬	⋄ ∘∘∘
∆_KdLT:	rWAT⍬	⋄ ∘∘∘
∆_KdRT:	rWAT⍬	⋄ ∘∘∘
∆_KuDN:	rWAT⍬	⋄ ∘∘∘
∆_KuUP:	rWAT⍬	⋄ ∘∘∘
∆_KuLT:	rWAT⍬	⋄ ∘∘∘
∆_KuRT:	rWAT⍬	⋄ ∘∘∘
∆_TICK:		⋄ →∆_
∆_XT:	SUCC⍬	⋄ →0
∆_ERR:	FAIL⍬	⋄ ∘∘∘
∆_WAT:	rWAT⍬ 	⋄ ∘∘∘

∆_CON_:→⎕LC+LISTEN⍬
∆_CON_CON:	CLOSE⍬ ⋄ REG⍬	⋄ →∆_CON_
∆_CON_WSU:	RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_KdDN:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdUP:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdLT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KdRT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuDN:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuUP:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuLT:	rWAT⍬		⋄ ∘∘∘
∆_CON_KuRT:	rWAT⍬		⋄ ∘∘∘
∆_CON_TICK:			⋄ →∆_CON_
∆_CON_XT:	SUCC⍬		⋄ →0
∆_CON_ERR:	FAIL⍬		⋄ ∘∘∘
∆_CON_WAT:	rWAT⍬ 		⋄ ∘∘∘

∆_CON_WSU_:→⎕LC+LISTEN⍬
∆_CON_WSU_CON:	CLOSE⍬ ⋄ REG⍬		⋄ →∆_CON_
∆_CON_WSU_WSU:	rWAT⍬			⋄ ∘∘∘
∆_CON_WSU_KdDN:	GODOWN⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KdUP:	GOUP⍬ ⋄ RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_WSU_KdLT:	GOLEFT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KdRT:	GORIGHT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuDN:	NODOWN⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuUP:	NOUP⍬ ⋄ RENDER⍬		⋄ →∆_CON_WSU_
∆_CON_WSU_KuLT:	NOLEFT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_KuRT:	NORIGHT⍬ ⋄ RENDER⍬	⋄ →∆_CON_WSU_
∆_CON_WSU_TICK:	RENDER⍬			⋄ →∆_CON_WSU_
∆_CON_WSU_XT:	SUCC⍬			⋄ →0
∆_CON_WSU_ERR:	FAIL⍬			⋄ ∘∘∘
∆_CON_WSU_WAT:	rWAT⍬ 			⋄ ∘∘∘
